buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

plugins {
    id "com.wynprice.cursemaven" version "2.1.1"
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'maven-publish'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

version = mc_version + '-' + ext.version
archivesBaseName = project.projectDir.name

minecraft {
    version = mc_version + '-' + forge_version
    runDir = "eclipse"
    mappings = "snapshot_${mcp_version}"
}

repositories {
    maven {
        name "TehNuuuuuuuuuuuut"
        url 'http://tehnut.info/maven/'
    }
    maven {
        name "TTerrag"
        url "https://maven.tterrag.com"
    }
    maven {
        name = "CoFH Maven"
        url = "http://maven.covers1624.net"
    }
    maven {
        name 'DVS1 Maven FS'
        url 'http://dvs1.progwml6.com/files/maven'
    }
}

dependencies {
    compile(project(':ProjectE-Integration'))

    // Tehnut
    runtime "info.amerifrance.guideapi:Guide-API:1.12-${version_guideapi}" //Woot Dependency

    //CoFH Maven
    deobfProvided "morph.avaritia:Avaritia:${version_avaritia}:deobf"
    runtime ("morph.avaritia:Avaritia:${version_avaritia}:universal") {
        exclude group: 'mezz.jei'
        exclude group: 'CraftTweaker2'
        exclude group: 'codechicken'
    }

    // TTerrag Maven
    deobfProvided "team.chisel:Chisel:MC${mc_version}-${version_chisel}"
    deobfProvided "team.chisel.ctm:CTM:MC${mc_version}-${version_ctm}"

    // DVS1 Maven FS
    provided "slimeknights.mantle:Mantle:1.12-1.3.3.42:deobf"
    runtime "slimeknights.mantle:Mantle:1.12-1.3.3.42"
    provided "slimeknights:TConstruct:${mc_version}-${version_tconstruct}:deobf"
    runtime "slimeknights:TConstruct:${mc_version}-${version_tconstruct}"

    // CurseMaven
    // --- deobfProvided "curse.maven:harvestcraft:${curse_harvestcraft}"
    //deobfProvided "curse.maven:harvestcraft:${curse_harvestcraft}"
    deobfProvided "curse.maven:woot:${curse_woot}"
}

task download_libraries {
    description = 'Download required libraries'
    doFirst {
        def lib = new File(projectDir, 'lib')
        lib.mkdir()
        println('Downloading Pam\'s HarvestCraft')
        def harvestcraft = new File(lib, 'harvestcraft.jar')
        if (!harvestcraft.exists()) {
            new URL('https://media.forgecdn.net/files/2904/825/Pam%27s+HarvestCraft+1.12.2zg.jar').withInputStream{ i -> harvestcraft.withOutputStream{ it << i }}
        }
    }
}

tasks['setupDecompWorkspace'].dependsOn(download_libraries)
tasks['setupDevWorkspace'].dependsOn(download_libraries)
tasks['setupCiWorkspace'].dependsOn(download_libraries)
