rootProject.name = 'ProjectE-Integration'

include 'ProjectE-Integration'
include 'Plugins'

def pei_plugins = []

def plugins_dir = new File(rootDir, 'Plugins')

def plugins = plugins_dir.listFiles(new FileFilter() {
    @Override
    boolean accept(File pathname) {
        if (!pathname.isDirectory()) {
            return false
        }
        if (pathname.name == 'buildSrc') {
            return false
        }
        return new File(pathname, 'build.gradle').isFile()
    }
})

plugins.each { File plugin ->
    include "Plugins:${plugin.name}"
    project(":Plugins:${plugin.name}").projectDir = new File(plugins_dir, plugin.name)
    pei_plugins += plugin.name
}

gradle.ext.pei_plugins = pei_plugins
