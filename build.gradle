buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven { url = "https://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

plugins {
  id "com.matthewprenger.cursegradle" version "1.1.2"
  id "se.bjurr.gitchangelog.git-changelog-gradle-plugin" version "1.32"
}

apply plugin: 'net.minecraftforge.gradle.forge'

version = "${version}"
if (System.getenv("BUILD_NUMBER") != null) {
    version += ".${System.getenv().BUILD_NUMBER}"
} else if (System.getenv("TRAVIS_BUILD_NUMBER") != null) {
    version += ".${System.getenv().TRAVIS_BUILD_NUMBER}"
}

if (System.getenv("TRAVIS_PULL_REQUEST") != null) {
    if (System.getenv("TRAVIS_PULL_REQUEST") != "false") {
    version = version + "+p" + System.getenv("TRAVIS_PULL_REQUEST");
    }
}

group = "com.tagnumelite.projecteintegration"
archivesBaseName = "ProjectEIntegration-${mc_version}"

sourceCompatibility = targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "${mc_version}-${forge_version}"
    runDir = "run"
    
    mappings = "snapshot_${mcp_version}"
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
    replace 'required-after:projecte', "required-after:projecte@[${pe_version},)"
    replace '@VERSION@', "${version}"
    
    //replace 'after:actuallyadditions', "after:actuallyadditions@[${version_actuallyadditions},)"
    //replace 'after:appliedenergistics2', "after:appliedenergistics2@[${version_appliedenergistics},)"
    //replace 'after:armorplus', "after:armorplus@[${version_armorplus},)"
    //replace 'after:avaritia', "after:avaritia@[${version_avaritia},)"
    //replace 'after:extendedcrafting', "after:extendedcrafting@[${version_extendedcrafting},)"
    //replace 'after:extrautils2', "after:extrautils2"
    replace 'after:mekanism', "after:mekanism@[${mc_version}-${version_mekanism},)"
}

repositories {
    maven {
        name "ModMaven"
        url "https://modmaven.k-4u.nl"
    }
    maven {
        name = "CurseForge"
        url = "https://minecraft.curseforge.com/api/maven/"
    }
    maven {
        name = "CoFH Maven"
        url = "http://maven.covers1624.net"
    }
}

dependencies {
    deobfCompile "projecte:ProjectE:${mc_version}:${pe_version}"
    
    deobfCompile "de.ellpeck.actuallyadditions:ActuallyAdditions:${version_actuallyadditions}"
    deobfProvided "appeng:appliedenergistics2:${version_appliedenergistics}:api"
    runtime ("appeng:appliedenergistics2:${version_appliedenergistics}") {
        exclude group: 'mezz.jei'
    }
    deobfProvided "morph.avaritia:Avaritia:${version_avaritia}:deobf"
    runtime ("morph.avaritia:Avaritia:${version_avaritia}:universal") {
        exclude group: 'mezz.jei'
        exclude group: 'CraftTweaker2'
        exclude group: 'codechicken', module: 'CodeChickenLib'
    }
    deobfCompile "vazkii.botania:Botania:${version_botania}"
    runtime "codechicken:CodeChickenLib:${version_codechickenlib}:universal"
    deobfCompile ("CraftTweaker2:CraftTweaker2-MC1120-Main:${version_crafttweaker}") {
        exclude group: 'mezz.jei'
    }
    deobfCompile ("com.brandon3055.draconicevolution:Draconic-Evolution:${version_draconicevolution}:deobf") {
        exclude group: 'mezz.jei'
        exclude group: 'codechicken', module: 'CodeChickenLib'
        exclude group: 'cofh', module: 'RedstoneFlux'
    }
    deobfCompile ("net.sengir.forestry:forestry_${mc_version}:${version_forestry}") {
        exclude group: 'mezz.jei'
    }
    deobfCompile ("blusunrize:ImmersiveEngineering:${version_immersiveengineering}") {
        exclude group: 'mezz.jei'
    }
    deobfProvided ("net.industrial-craft:industrialcraft-2:${version_idustrialcraft}") {
        exclude group: 'mezz.jei'
    }
    deobfProvided "mezz.jei:jei_${mc_version}:${version_jei}:api"
    runtime "mezz.jei:jei_${mc_version}:${version_jei}"
    provided "slimeknights.mantle:Mantle:1.12-1.3.3.42:deobf"
    runtime "slimeknights.mantle:Mantle:1.12-1.3.3.42"
    deobfCompile "vazkii.psi:Psi:${version_psi}"
    deobfCompile ("RebornCore:RebornCore-${mc_version}:${version_reborncore}:universal") {
        exclude group: 'mezz.jei'
    }
    deobfCompile "cofh:RedstoneFlux:${version_redstoneflux}:universal"
    runtime ("cofh:CoFHCore:${version_cofhcore}:universal") {
        exclude group: 'mezz.jei'
        exclude group: 'cofh', module: 'RedstoneFlux'
    }
    runtime ("cofh:CoFHWorld:${version_cofhworld}:universal") {
        exclude group: 'mezz.jei'
        exclude group: 'cofh', module: 'RedstoneFlux'
    }
    deobfCompile "TechReborn:TechReborn-${mc_version}:${version_techreborn}:universal"
    deobfCompile ("cofh:ThermalExpansion:${version_thermalexpansion}:universal") {
        exclude group: 'mezz.jei'
        exclude group: 'codechicken', module: 'CodeChickenLib'
    }
    deobfCompile ("cofh:ThermalFoundation:${version_thermalfoundation}:universal") {
        exclude group: 'mezz.jei'
    }
    provided "slimeknights:TConstruct:${mc_version}-${version_tconstruct}:deobf"
    runtime "slimeknights:TConstruct:${mc_version}-${version_tconstruct}"
    
    // CurseForge
    deobfCompile "astral-sorcery:astralsorcery:${version_astralsorcery}"
    deobfCompile "armorplus:armorplus:${mc_version}:${version_armorplus}"
    runtime "baubles:Baubles:1.12:1.5.2"
    deobfCompile "blood-magic:BloodMagic-${mc_version}:${version_bloodmagic}"
    deobfCompile "calculator:calculator-${mc_version}:${version_calculator}"
	deobfCompile "charset-lib:Charset:Lib:${version_charsetlib}"
    deobfCompile "compact-machines:compactmachines3-${mc_version}:${version_compactmachines}"
    runtime "cucumber:Cucumber:${mc_version}:${version_cucumber}" //ExtendedCrafting & Mystical-Agriculture Dependency
	deobfCompile "embers:embers:${version_embers}"
    //deobfCompile "embers-rekindled:EmbersRekindled:${version_embersrekindled}" //Switch to embers
    deobfProvided "endercore:EnderCore:${mc_version}:${version_endercore}"
    deobfProvided "ender-io:EnderIO:${mc_version}:${version_enderio}"
    deobfCompile "extended-crafting:ExtendedCrafting:${mc_version}:${version_extendedcrafting}"
    deobfCompile "extrabotany:ExtraBotany:${version_extrabotany}"
    runtime "guide-api:Guide-API-1.12:${version_guideapi}" //Blood Magic & Woot Dependency
    deobfCompile "mekanism:Mekanism:${mc_version}:${version_mekanism}"
    deobfCompile "mystical-agriculture:MysticalAgriculture:${mc_version}:${version_mysticalagriculture}"
    deobfCompile "nuclearcraft-mod:NuclearCraft-${version_nuclearcraft}-:${mc_version}"
    deobfCompile "sonar-core:sonarcore-${mc_version}:${version_sonarcore}" //Calculator Dependency
    deobfCompile "thaumcraft:Thaumcraft:${mc_version}:${version_thaumcraft}"
    runtime "thedragonlib:thedragonlib:${mc_version}:${version_thedragonlib}" //ArmorPlus Dependency
    deobfCompile "woot:woot:${mc_version}:${version_woot}"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
    
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task storeVersion {
    ext.version_file = new File("version.txt")
    ext.version_file.text = "MOD_VERSION=" + version
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    from sourceSets.main.java
    classifier = 'deobf'
}

task apiJar(type: Jar) {
    from sourceSets.main.output
    from sourceSets.main.java
    classifier = 'api'
    include 'com/tagnumelite/projecteintegration/api/**'
}

javadoc {
    include 'com/tagnumelite/projecteintegration/api/**'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from 'build/docs/javadoc'
    classifier 'javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.java
    classifier = 'sources'
}

jar {}

artifacts {
    archives apiJar
    archives javadocJar
    archives sourcesJar
    archives deobfJar
}

runClient {
    jvmArgs "-Xmx6G", "-Xms6G"
}

curseforge {
    apiKey = System.env.CURSEFORGE_API_KEY != null ? System.env.CURSEFORGE_API_KEY : '0'
    project {
        id = "${cf_project}"
        changelog = file('changelog.html')
        changelogType = 'html'
        releaseType = "${release_type}"
        addGameVersion "${mc_version}"
        mainArtifact(jar) {
          displayName = "ProjectE Integration - ${version}"
        }
        addArtifact(deobfJar) {
          displayName = "ProjectE Integration - ${version} - deobf"
        }
        addArtifact(apiJar) {
          displayName = "ProjectE Integration - ${version} - api"
        }
        addArtifact(sourcesJar) {
          displayName = "ProjectE Integration - ${version} - sources"
        }
        relations {
            requiredDependency 'ProjectE'
            
            // From Other Mavens
            optionalLibrary 'Actually-Additions'
            optionalLibrary 'Applied-Energistics-2'
            optionalLibrary 'Avaritia-1-10'
            optionalLibrary 'Botania'
            optionalLibrary 'CraftTweaker'
            optionalLibrary 'Draconic-Evolution'
            optionalLibrary 'Forestry'
            optionalLibrary 'Immersive-Engineering'
            optionalLibrary 'Industrial-Craft'
            optionalLibrary 'RebornCore'
            optionalLibrary 'TechReborn'
            optionalLibrary 'ThermalExpansion'
            optionalLibrary 'Tinkers-Construct'
            
            // From CurseForge Maven
            optionalLibrary 'Astral-Sorcery'
            optionalLibrary 'ArmorPlus'
            optionalLibrary 'Blood-Magic'
            optionalLibrary 'Calculator'
            optionalLibrary 'Charset-Lib'
            optionalLibrary 'Compact-Machines'
            optionalLibrary 'Embers'
            optionalLibrary 'Ender-IO'
            optionalLibrary 'Extended-Crafting'
            optionalLibrary 'ExtraBotany'
            optionalLibrary 'Mekanism'
            optionalLibrary 'Mystical-Agriculture'
            optionalLibrary 'NuclearCraft-mod'
            optionalLibrary 'Pams-HarvestCraft'
            optionalLibrary 'Thaumcraft'
            optionalLibrary 'Woot'
        }
    }
}

task signJar(type: SignJar, dependsOn: reobfJar) {
    onlyIf {
        project.hasProperty('keyStore')
    }
    keyStore = project.findProperty('keyStore')
    alias = project.findProperty('keyStoreAlias')
    storePass = project.findProperty('keyStorePass')
    keyPass = project.findProperty('keyStoreKeyPass')
    inputFile = jar.archivePath
    outputFile = jar.archivePath
}

task signDeobfJar(type: SignJar, dependsOn: deobfJar) {
    onlyIf {
        project.hasProperty('keyStore')
    }
    keyStore = project.findProperty('keyStore')
    alias = project.findProperty('keyStoreAlias')
    storePass = project.findProperty('keyStorePass')
    keyPass = project.findProperty('keyStoreKeyPass')
    inputFile = deobfJar.archivePath
    outputFile = deobfJar.archivePath
}

task signApiJar(type: SignJar, dependsOn: apiJar) {
    onlyIf {
        project.hasProperty('keyStore')
    }
    keyStore = project.findProperty('keyStore')
    alias = project.findProperty('keyStoreAlias')
    storePass = project.findProperty('keyStorePass')
    keyPass = project.findProperty('keyStoreKeyPass')
    inputFile = apiJar.archivePath
    outputFile = apiJar.archivePath
}

task signSrcJar(type: SignJar, dependsOn: sourcesJar) {
    onlyIf {
        project.hasProperty('keyStore')
    }
    keyStore = project.findProperty('keyStore')
    alias = project.findProperty('keyStoreAlias')
    storePass = project.findProperty('keyStorePass')
    keyPass = project.findProperty('keyStoreKeyPass')
    inputFile = sourcesJar.archivePath
    outputFile = sourcesJar.archivePath
}

task makeChangelog(type: se.bjurr.gitchangelog.plugin.gradle.GitChangelogTask) {
    filePath = "changelog.html"
    untaggedName = "Current release ${project.version}"
    fromCommit = "939adc1b1975da6b89486d1e5f02e9e18027f0c2"
    toRef =  "HEAD"
    templateContent = """
{{#tags}}
  <h3>{{name}}</h3>
  <ul>
    {{#commits}}
    <li>{{{message}}}</li>
    {{/commits}}
  </ul>
{{/tags}}
"""
}

build.dependsOn signJar
build.dependsOn signDeobfJar
build.dependsOn signSrcJar
build.dependsOn signApiJar

afterEvaluate {
    tasks["curseforge${cf_project}"].dependsOn.add(signJar)
    tasks["curseforge${cf_project}"].dependsOn.add(signDeobfJar)
    tasks["curseforge${cf_project}"].dependsOn.add(signApiJar)
    tasks["curseforge${cf_project}"].dependsOn.add(signSrcJar)
    tasks["curseforge${cf_project}"].dependsOn.add(makeChangelog)
}
