/*
 * Copyright (c) 2019-2020 TagnumElite
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */
import groovy.json.JsonSlurper
import groovy.json.JsonOutput

buildscript {
    repositories {
        jcenter()
        maven { url = "https://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

plugins {
    id 'com.wynprice.cursemaven' version '2.1.1' apply false
    id 'com.matthewprenger.cursegradle' version '1.4.0'
}

apply plugin: 'java'

version = "${version}"
if (System.getenv('GITHUB_ACTIONS') != null && System.getenv('GITHUB_EVENT_NAME') != 'release') {
    version += "+${System.getenv('GITHUB_SHA').substring(0, 7)}"
}

group = "com.tagnumelite.projecteintegration"
archivesBaseName = "ProjectEIntegration-${mc_version}"

task wrapper(type: Wrapper, description: 'Creates and deploys the Gradle wrapper to the current directory.') {
    gradleVersion = '4.9'
}

def projects = ['ProjectE-Integration', 'Plugins']

subprojects {
    sourceCompatibility = targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.
    compileJava {
        sourceCompatibility = targetCompatibility = '1.8'
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            name = 'MinecraftForge'
            url = 'http://files.minecraftforge.net/maven'
        }
        maven {
            name = 'CurseForge'
            url = 'https://minecraft.curseforge.com/api/maven/'
        }
        maven {
            name = 'ModMaven'
            url = 'https://modmaven.k-4u.nl'
        }
        maven {
            name = 'JitPack'
            url = 'https://jitpack.io'
        }
    }
}

subprojects.each { subproject -> evaluationDependsOn(subproject.path) }

task setupDecompWorkspace(dependsOn: projects.collect { ':' + it + ':setupDecompWorkspace' }) {}
task setupCiWorkspace(dependsOn: projects.collect { ':' + it + ':setupCiWorkspace' }) {}
task setupDevWorkspace(dependsOn: projects.collect { ':' + it + ':setupDevWorkspace' }) {}

task build(overwrite: true, type: Jar, dependsOn: projects.collect { ':' + it + ':build' }) {
    for (plugin in projects) {
        from zipTree(project(plugin).jar.archivePath)
    }
}

task sourcesJar(type: Jar, dependsOn: projects.collect { ':' + it + ':sourcesJar' }) {
    description = 'Creates a JAR containing the source code.'
    for (plugin in projects) {
        from zipTree(project(plugin).sourcesJar.archivePath)
    }
    classifier = 'sources'
}

task deobfJar(type: Jar, dependsOn: projects.collect { ':' + it + ':deobfJar' }) {
    description = 'Creates a JAR containing the non-obfuscated compiled code.'
    for (plugin in projects) {
        from zipTree(project(plugin).deobfJar.archivePath)
    }
    classifier = "deobf"
}

task javadocJar(type: Jar, dependsOn: projects.collect { ':' + it + ':javadocJar' }) {
    description = 'Creates a JAR containing the JavaDocs.'
    for (plugin in projects) {
        from zipTree(project(plugin).javadocJar.archivePath)
    }
    classifier = 'javadoc'
}

task apiJar(type: Jar, dependsOn: ':ProjectE-Integration:apiJar') {
    description = 'Creates a JAR containing the API'
    from zipTree(project(':ProjectE-Integration').apiJar.archivePath)
    classifier = 'api'
}

artifacts {
    archives sourcesJar
    archives deobfJar
    archives javadocJar
    archives apiJar
}

task updateJSON() {
    def jsonFile = file('update.json')
    def parsedJson = new JsonSlurper().parse(jsonFile)
    parsedJson."${mc_version}"."${version}" = "See https://github.com/TagnumElite/ProjectE-Integration/releases/${version} for more detail"
    parsedJson.promos."${mc_version}-latest" = "${version}"
    parsedJson.promos."${mc_version}-recommended" = "${version}"
    jsonFile.write(JsonOutput.prettyPrint(JsonOutput.toJson(parsedJson)))
}

build.dependsOn sourcesJar
build.dependsOn deobfJar
build.dependsOn javadocJar
build.dependsOn apiJar

curseforge {
    options {
        debug = true
    }
    apiKey = System.env.CURSEFORGE_API_KEY != null ? System.env.CURSEFORGE_API_KEY : '0'
    project {
        id = "${cf_project}"
        changelog = ""
        changelogType = 'markdown'
        releaseType = "${release_type}"
        addGameVersion "${mc_version}"
        mainArtifact(jar) { displayName = "ProjectEIntegration-${mc_version}-${version}" }
        addArtifact(apiJar) { displayName = "ProjectEIntegration-${mc_version}-${version}-api" }
        addArtifact(deobfJar) { displayName = "ProjectEIntegration-${mc_version}-${version}-deobf" }
        addArtifact(javadocJar) { displayName = "ProjectEIntegration-${mc_version}-${version}-javadoc" }
        addArtifact(sourcesJar) { displayName = "ProjectEIntegration-${mc_version}-${version}-sources" }
        relations {
            requiredDependency 'ProjectE'

            // ProjectE-Integration Compact
            optionalLibrary 'CraftTweaker'

            // Crafting Plugins
            optionalLibrary 'ArmorPlus'
            optionalLibrary 'Artisan-Worktables'
            optionalLibrary 'Chisel'
            optionalLibrary 'Extended-Crafting'
            optionalLibrary 'Modular-Machinery'

            // Duplicate Plugins

            // Legacy Plugins
            //optionalLibrary 'IC2Classic'
            optionalLibrary 'NuclearCraft-mod'

            // Library Plugins
            optionalLibrary 'Charset-Lib'
            optionalLibrary 'LibVulpes'
            optionalLibrary 'RebornCore'
            optionalLibrary 'SonarCore'

            // Magic Plugins
            optionalLibrary 'Astral-Sorcery'
            optionalLibrary 'Blood-Magic'
            optionalLibrary 'Botania'
            optionalLibrary 'Embers'
            optionalLibrary 'Embers-Rekindled' //The embers plugin supports both
            optionalLibrary 'ExtraBotany'
            optionalLibrary 'Mystical-Agriculture'
            optionalLibrary 'Psi'
            optionalLibrary 'Thaumcraft'

            // Misc Plugins
            optionalLibrary 'Avaritia-1-10'
            optionalLibrary 'Pams-HarvestCraft'
            optionalLibrary 'Tinkers-Construct'
            optionalLibrary 'Woot'

            // Rocketry Plugins
            optionalLibrary 'Advanced-Rocketry'

            // Tech Plugins
            optionalLibrary 'Actually-Additions'
            optionalLibrary 'Applied-Energistics-2'
            optionalLibrary 'Calculator'
            optionalLibrary 'Compact-Machines'
            optionalLibrary 'Draconic-Evolution'
            optionalLibrary 'Ender-IO'
            optionalLibrary 'Forestry'
            optionalLibrary 'GregTechCE'
            optionalLibrary 'Immersive-Engineering'
            optionalLibrary 'Industrial-Craft'
            optionalLibrary 'Mekanism'
            optionalLibrary 'NuclearCraft-Overhauled'
            optionalLibrary 'PneumaticCraft-Repressurized'
            optionalLibrary 'TechReborn'
            optionalLibrary 'Thermal-Expansion'
        }
    }
}

afterEvaluate {
    tasks["curseforge${cf_project}"].dependsOn.add(apiJar)
    tasks["curseforge${cf_project}"].dependsOn.add(deobfJar)
    tasks["curseforge${cf_project}"].dependsOn.add(javadocJar)
    tasks["curseforge${cf_project}"].dependsOn.add(sourcesJar)
    tasks["curseforge${cf_project}"].dependsOn.add(updateJSON)
}

