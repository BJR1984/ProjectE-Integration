buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven { url = "https://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

plugins {
    id "com.wynprice.cursemaven" version "2.1.1"
}

apply plugin: 'net.minecraftforge.gradle.forge'
//Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.
apply plugin: 'maven-publish'

version = ext.version
//group = "com.tagnumelite.projecteintegration"
//archivesBaseName = "ProjectEIntegration-${mc_version}"
archivesBaseName = project.projectDir.name

sourceCompatibility = targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

jar {

}

minecraft {
    version = "${mc_version}-${forge_version}"
    runDir = "run"

    mappings = "snapshot_${mcp_version}"

    replace 'required-after:projecte', "required-after:projecte@[${pe_version},)"
    replace '@VERSION@', "${version}"

    replace 'after:mekanism', "after:mekanism@[${mc_version}-1.0.0,${mc_version}-1.2.0],[${mc_version}-${version_mekanism},)"
    replace 'after:nuclearcraft', "after:nuclearcraft@[${version_nuclearcraft},)"
}

repositories {
    maven {
        name "TTerrag"
        url "https://maven.tterrag.com"
    }
    maven {
        name "TehNuuuuuuuuuuuut"
        url 'http://tehnut.info/maven/'
    }
}

dependencies {
    deobfProvided "curse.maven:projecte:${pe_id}"

    deobfProvided "CraftTweaker2:CraftTweaker2-MC1120-Main:${version_crafttweaker}"
    deobfProvided "mezz.jei:jei_${mc_version}:${version_jei}:api"
    runtime "mezz.jei:jei_${mc_version}:${version_jei}"
    provided "slimeknights.mantle:Mantle:1.12-1.3.3.42:deobf"
    runtime "slimeknights.mantle:Mantle:1.12-1.3.3.42"

    // CurseForge
    deobfProvided "curse.maven:extrautilities:${curse_extrautilities}"
    runtime "curse.maven:baubles:${curse_baubles}"
    deobfProvided "curse.maven:nuclearcraftoverhauled:${curse_nuclearcraftoverhauled}"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    description = 'Creates a JAR containing the source code.'
    from sourceSets.main.allSource
    classifier = 'sources'
}

task deobfJar(type: Jar) {
    description = 'Creates a JAR containing the non-obfuscated compiled code.'
    from sourceSets.main.output
    classifier = "deobf"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    description = 'Creates a JAR containing the JavaDocs.'
    from javadoc.destinationDir
    classifier = 'javadoc'
}

artifacts {
    archives sourcesJar
    archives deobfJar
    archives javadocJar
}

runClient {
    jvmArgs "-Xmx6G", "-Xms6G"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            from components.java

            // Adds the various artifacts
            artifact sourcesJar {

                classifier 'sources'
            }
            artifact javadocJar {

                classifier 'javadoc'
            }
            artifact deobfJar {

                classifier 'deobf'
            }
        }
    }

    repositories {

        maven {

            url "file://" + System.getenv("local_maven")
        }
    }
}
